#!/usr/bin/perl

use strict;
use warnings;
use TAP::Parser;
use TAP::Parser::Aggregator;
use TAP::Formatter::JUnit;
use Getopt::Long;
use Pod::Usage;
use IO::File;
use File::Slurp qw(slurp);

###############################################################################
# Read in our command line options.
my ($help, $man, $test_name);
my $verbose;
my $suffix = '.xml';
GetOptions(
    'suffix=s'     => \$suffix,
    'verbose'      => \$verbose,
    'help|?'       => \$help,
    'man'          => \$man,
    'junit_name=s' => \$test_name,
) || pod2usage(1);
pod2usage(1) if ($help);
pod2usage( -exitstatus=>0, -verbose=>2 ) if ($man);

###############################################################################
# Get the names of all of the TAP files we're supposed to convert.
my @tap_files = @ARGV;
pod2usage(1) unless (@tap_files);

###############################################################################
# Convert all of the TAP files to JUnit.
foreach my $file (@tap_files) {
    verbose( "converting TAP to JUnit: $file" );

    my $tap;
    # Slurp in the TAP.
    if ( $file eq '-' ) {
        $tap = join('',<STDIN>);
    }
    else {
        $tap = slurp($file);
    }

    # Open up a FH for where we're going to dump the JUnit
    my $fout;
    if ($file ne '-') {
        my $junit_file = "${file}${suffix}";
        $fout = IO::File->new( $junit_file, '>' )
                || die "can't open '$junit_file' for writing; $!";
    }
    else {
        open($fout,  ">&STDOUT") or die "Can't dup STDOUT: $!\n";
    }

    # Convert the TAP to JUnit
    eval {
        # Create the TAP formatter, aggregator, and parser that we're going to
        # use to convert the TAP to JUnit.
        my $formatter  = TAP::Formatter::JUnit->new( { stdout => $fout } );
        my $aggregator = TAP::Parser::Aggregator->new();
        my $parser     = TAP::Parser->new( { tap => $tap } );

        # Parse all of the TAP in this file.
        $aggregator->start();

        # Use the --junit_name if we're reading from STDIN.
        $file = $test_name if $file eq '-' and defined $test_name;
        my $session = $formatter->open_test($file, $parser);
        while (my $result = $parser->next()) {
            $session->result($result);
        }
        $session->close_test();
        $aggregator->add($file, $parser);

        $aggregator->stop();

        # Summarize the results (in JUnit)
        $formatter->summary($aggregator);
    };
    if ($@) {
        warn $@;
    }

    $fout->close();
}

###############################################################################
# All done; exit peacefully.
exit;

sub verbose {
    print "$_[0]\n" if ($verbose);
}


=head1 NAME

tap2junit - Converts TAP output to JUnit

=head1 SYNOPSIS

  tap2junit [options] <filename> <filename> ...

  Options:
    --suffix <suffix>   Suffix for JUnit output files (default ".xml")
    --verbose           Display verbose status during conversion
    --help/-?           Display brief help message
    --man               Display full documentation
    --junit_name <name> Name to be inserted into the JUnit XML as the
                        test name when reading from STDIN (see below)

=head1 DESCRIPTION

C<tap2junit> converts TAP output to JUnit.

Give it a list of files containing TAP results and it will create a series of
F<????.junit.xml> output files containing the JUnit representations of that TAP
contained in the files.

If you specify '-' as a filename, it will read from STDIN and write to STDOUT.
The '--junit_name' option allows you to assign a test name that will be
inserted into the output JUnit XML; if you don't specify this when using '-'
as the filename, tap2junit will go ahead and use '-' as the name of the test.

=head1 OPTIONS

=over

=item B<--suffix E<lt>suffixE<gt>>

Specifies the suffix which is appended to all of the input files, in order to
generate the filename for the JUnit XML file that is being output.

If you want to live dangerously and over-write your original TAP files, you can
set this to "" and your original TAP files will be over-written.

Defaults to F<.xml>

=item B<--verbose>

Display verbose status information during the conversion (telling you which TAP
file its working on).

=item B<--help/-?>

Display brief help message.

=item B<--man>

Displays the full documentation.

=back

=head1 AUTHOR

Graham TerMarsch <cpan@howlingfrog.com>

=head1 COPYRIGHT

Copyright 2008-2009, Graham TerMarsch.  All Rights Reserved.

This is free software; you can redistribute it and/or modify it under the same
terms as Perl itself.

=head1 SEE ALSO

L<TAP::Formatter::JUnit>.

=cut
